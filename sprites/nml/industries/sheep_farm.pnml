/*
 * This file is part of FIRS industry set.
 * FIRS industry set is free software; you can redistribute it and/or modify it under the terms of the GNU General Public License as published by the Free Software Foundation, version 2.
 * FIRS industry set is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
 * See the GNU General Public License for more details. You should have received a copy of the GNU General Public License along with FIRS industry set. If not, see <http://www.gnu.org/licenses/>.
 */

#include "../templates/spritelayout_templates.pnml"

#define THIS_ID(...) sheep_farm ## __VA_ARGS__

spriteset(THIS_ID(spriteset_1)) {
	tmpl_building_sprite_filename(10, 10, 52, -21, "sprites/graphics/industries/sheepfarm.png")
	tmpl_building_sprite_filename(10, 10, 52, -21, "sprites/graphics/industries/sheepfarm_snow.png")
}

spriteset(THIS_ID(spriteset_2)) {
	tmpl_building_sprite_filename(80, 10, 52, -19, "sprites/graphics/industries/sheepfarm.png")
	tmpl_building_sprite_filename(80, 10, 52, -19, "sprites/graphics/industries/sheepfarm_snow.png")
}

spriteset(THIS_ID(spriteset_3)) {
	tmpl_building_sprite_filename(150, 10, 52, -21, "sprites/graphics/industries/sheepfarm.png")
	tmpl_building_sprite_filename(150, 10, 52, -21, "sprites/graphics/industries/sheepfarm_snow.png")
}

spriteset(THIS_ID(spriteset_4)) {
	tmpl_building_sprite_filename(220, 10, 52, -21, "sprites/graphics/industries/sheepfarm.png")
	tmpl_building_sprite_filename(220, 10, 52, -21, "sprites/graphics/industries/sheepfarm_snow.png")
}

spriteset(THIS_ID(spriteset_5)) {
	tmpl_building_sprite_filename(290, 10, 52, -21, "sprites/graphics/industries/sheepfarm.png")
	tmpl_building_sprite_filename(290, 10, 52, -21, "sprites/graphics/industries/sheepfarm_snow.png")
}

SPRITELAYOUT_NORMAL_SNOW(THIS_ID(spritelayout_1), GROUNDSPRITE_NORMAL, GROUNDSPRITE_NORMAL, THIS_ID(spriteset_1), 24)

SPRITELAYOUT_NORMAL_SNOW(THIS_ID(spritelayout_2), GROUNDSPRITE_NORMAL, GROUNDSPRITE_NORMAL, THIS_ID(spriteset_2), 24)

SPRITELAYOUT_NORMAL_SNOW(THIS_ID(spritelayout_3), GROUNDSPRITE_NORMAL, GROUNDSPRITE_NORMAL, THIS_ID(spriteset_3), 24)

SPRITELAYOUT_NORMAL_SNOW(THIS_ID(spritelayout_4), GROUNDSPRITE_NORMAL, GROUNDSPRITE_NORMAL, THIS_ID(spriteset_4), 8)

SPRITELAYOUT_NORMAL_SNOW(THIS_ID(spritelayout_5), GROUNDSPRITE_NORMAL, GROUNDSPRITE_NORMAL, THIS_ID(spriteset_5), 8)

switch(FEAT_INDUSTRYTILES, SELF, THIS_ID(layout_1), relative_pos) {
	relative_coord(0, 0): THIS_ID(spritelayout_3);
	relative_coord(1, 0): THIS_ID(spritelayout_2);
	relative_coord(1, 2): THIS_ID(spritelayout_4);
	relative_coord(3, 0): THIS_ID(spritelayout_1);
	THIS_ID(spritelayout_5);
}

switch(FEAT_INDUSTRYTILES, SELF, THIS_ID(layout_2), relative_pos) {
	relative_coord(0, 0): THIS_ID(spritelayout_2);
	relative_coord(0, 1): THIS_ID(spritelayout_1);
	relative_coord(0, 2): THIS_ID(spritelayout_4);
	relative_coord(2, 0): THIS_ID(spritelayout_3);
	THIS_ID(spritelayout_5);
}

switch(FEAT_INDUSTRYTILES, PARENT, THIS_ID(layout), layout_num) {
	2: THIS_ID(layout_2);
	THIS_ID(layout_1);
}

switch(FEAT_INDUSTRYTILES, SELF, THIS_ID(tile1_layout), STORE_TEMP((var[0x7E, 0, 255, 161] + var[0x7E, 0, 255, 65]), 16)) {
	THIS_ID(layout);
}

/* *************************************************
 * Definition of the industry tile and its callbacks
 * *************************************************/

TILE_DISALLOW_NEARBY_CLASS(tile1_nearby_industry, TILE_CLASS_INDUSTRY, CB_RESULT_LOCATION_DISALLOW, return CB_RESULT_LOCATION_ALLOW)
TILE_ALLOW_PLAYER         (tile1_player_check, THIS_ID(tile1_nearby_industry))
TILE_DISALLOW_COAST       (tile1_location_check, CB_RESULT_LOCATION_DISALLOW, THIS_ID(tile1_player_check))

item(FEAT_INDUSTRYTILES, THIS_ID(tile), TILE_SHEEPFARM_1) {
	property {
		substitute: 0;
		accepted_cargos: [[FMSP, 8]];
		land_shape_flags: 0;
	}

	graphics {
		tile_check:   THIS_ID(tile1_location_check);
		THIS_ID(tile1_layout);
	}
}

/* *************************************************
 * Definition of the industry layouts
 * *************************************************/

tilelayout tilelayout_sheep_farm_1 {
	0, 0: THIS_ID(tile);
	1, 0: THIS_ID(tile);
	3, 0: THIS_ID(tile);
	3, 1: THIS_ID(tile);
	1, 2: THIS_ID(tile);
}

tilelayout tilelayout_sheep_farm_2 {
	0, 0: THIS_ID(tile);
	0, 1: THIS_ID(tile);
	0, 2: THIS_ID(tile);
	2, 0: THIS_ID(tile);
	2, 2: THIS_ID(tile);
}

/* *************************************************
 * Definition of the industry
 * *************************************************/

#define THIS_STR_EXTRA_SUPPLIES        STR_EXTRA_FARM_SUPPLIES

CHECK_NEARBY_CLUSTER(sheep_farm,    10, 36, 1, 2, return CB_RESULT_LOCATION_DISALLOW, return CB_RESULT_LOCATION_ALLOW)
CHECK_INCOMPATIBLE  (stockyard,     16, CB_RESULT_LOCATION_DISALLOW, THIS_ID(sheep_farm))
CHECK_INCOMPATIBLE  (textile_mill,  16, CB_RESULT_LOCATION_DISALLOW, THIS_ID(stockyard))
CHECK_FOUNDER       (THIS_ID(textile_mill))

SWITCH_COLOUR_SCHEME1(THIS_ID(colour))

#include "../templates/produce_primary.pnml"
#include "../templates/check_primary_production_level.pnml"

#include "../templates/extra_text_primary.pnml"

#include "../templates/check_availability.pnml"

item(FEAT_INDUSTRIES, sheep_farm, IND_SHEEPFARM_ID) {
	property {
		substitute: 0;
		layouts: [
				tilelayout_sheep_farm_1,
				tilelayout_sheep_farm_2
			];
		life_type:              IND_LIFE_TYPE_ORGANIC;
		closure_msg:            TTD_STR_NEWS_INDUSTRY_CLOSURE_SUPPLY_PROBLEMS;
		prod_increase_msg:      TTD_STR_NEWS_INDUSTRY_PRODUCTION_INCREASE_FARM;
		prod_decrease_msg:      TTD_STR_NEWS_INDUSTRY_PRODUCTION_DECREASE_GENERAL;
		new_ind_msg:            TTD_STR_NEWS_INDUSTRY_CONSTRUCTION;
		name:                   string(STR_IND_SHEEPFARM);
		nearby_station_name:    string(STR_STATION, string(STR_TOWN), string(STR_IND_SHEEPFARM));
		prod_cargo_types: [LVST, WOOL];
		accept_cargo_types: [FMSP];
		prod_multiplier: [4, 4];
		input_multiplier_1: [0, 0];
		input_multiplier_2: [0, 0];
		input_multiplier_3: [0, 0];
		min_cargo_distr: 1;
		conflicting_ind_types: [];
		prob_random: 6;
		prob_in_game: 3;
		prospect_chance: 0.75;
		fund_cost_multiplier: 45;
		map_colour: 14;
		remove_cost_multiplier: 0;
		spec_flags:             0;
	}
	graphics {
		produce_cargo_arrival:   THIS_ID(produce);
		construction_probability:THIS_ID(check_availability);
		location_check:          THIS_ID(check_location);
		monthly_prod_change:     THIS_ID(check_production_level);
		random_prod_change:      THIS_ID(check_closure);
		build_prod_change:       randomise_primary_production_on_build;
		extra_text_industry:     THIS_ID(extra_text);
		colour:                  THIS_ID(colour);
	}
}

#include "../templates/undefs.pnml"

