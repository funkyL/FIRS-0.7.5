/*
 * This file is part of FIRS Industry Set for OpenTTD.
 * FIRS is free software; you can redistribute it and/or modify it under the terms of the GNU General Public License as published by the Free Software Foundation, version 2.
 * FIRS is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
 * See the GNU General Public License for more details. You should have received a copy of the GNU General Public License along with FIRS. If not, see <http://www.gnu.org/licenses/>.
 */

#include "../templates/spritelayout_templates.pnml"

#define THIS_ID(...) lumber_yard ## __VA_ARGS__

spriteset(THIS_ID(spriteset_1), "sprites/graphics/industries/lumberyard.png") { tmpl_building_sprite(80, 10, 64, -40) }
spriteset(THIS_ID(spriteset_2), "sprites/graphics/industries/lumberyard.png") { tmpl_building_sprite(150, 10, 64, -34) }
spriteset(THIS_ID(spriteset_4), "sprites/graphics/industries/lumberyard.png") { tmpl_building_sprite(290, 10, 64, -35) }
spriteset(THIS_ID(spriteset_5), "sprites/graphics/industries/lumberyard.png") { tmpl_building_sprite(10, 10, 64, -26) }
spriteset(THIS_ID(spriteset_6), "sprites/graphics/industries/lumberyard.png") { tmpl_building_sprite(150, 90, 31, -4) }
spriteset(THIS_ID(spriteset_7), "sprites/graphics/industries/lumberyard.png") { tmpl_building_sprite(220, 90, 31, -4) }
spriteset(THIS_ID(spriteset_8), "sprites/graphics/industries/lumberyard.png") { tmpl_building_sprite(290, 90, 31, -4) }
spriteset(THIS_ID(spriteset_9), "sprites/graphics/industries/lumberyard.png") { tmpl_building_sprite(360, 10, 64, -24) }

SPRITELAYOUT_FENCES_NORMAL(THIS_ID(spritelayout_1), 2022, THIS_ID(spriteset_1), 16)

SPRITELAYOUT_FENCES_NORMAL(THIS_ID(spritelayout_2), 2022, THIS_ID(spriteset_2), 16)

SPRITELAYOUT_FENCES_NORMAL(THIS_ID(spritelayout_large_shed), 2022, THIS_ID(spriteset_4), 16)

SPRITELAYOUT_FENCES_NORMAL(THIS_ID(spritelayout_5), 2022, THIS_ID(spriteset_5), 16)

SPRITELAYOUT_FENCES_NORMAL(THIS_ID(spritelayout_6), 2022, THIS_ID(spriteset_6), 16)

SPRITELAYOUT_FENCES_NORMAL_BEGIN(THIS_ID(spritelayout_7), 2022, THIS_ID(spriteset_7), 16)
	building {
		sprite: THIS_ID(spriteset_7);
		recolour_mode: RECOLOUR_REMAP;
		palette: 0;
	}
SPRITELAYOUT_FENCES_END

SPRITELAYOUT_FENCES_NORMAL(THIS_ID(spritelayout_8), 2022, THIS_ID(spriteset_8), 16)

spritelayout THIS_ID(spritelayout_9) {
	ground {
		sprite: 2022;
	}
}

switch(FEAT_INDUSTRYTILES, SELF, THIS_ID(layout_1), relative_pos) {
	relative_coord(0, 0): THIS_ID(spritelayout_2);
	relative_coord(0, 1): THIS_ID(spritelayout_1);
	relative_coord(0, 2): THIS_ID(spritelayout_5);
	relative_coord(1, 0): THIS_ID(spritelayout_6);
	relative_coord(1, 1): THIS_ID(spritelayout_large_shed);
	relative_coord(2, 0): THIS_ID(spritelayout_8);
	relative_coord(2, 1): THIS_ID(spritelayout_large_shed);
	relative_coord(1, 2): THIS_ID(spritelayout_8);
	relative_coord(2, 2): THIS_ID(spritelayout_7);
	relative_coord(3, 0): THIS_ID(spritelayout_6);
	relative_coord(3, 1): THIS_ID(spritelayout_5);
	relative_coord(3, 2): THIS_ID(spritelayout_6);
	THIS_ID(spritelayout_9);
}

switch(FEAT_INDUSTRYTILES, SELF, THIS_ID(layout_2), relative_pos) {
	relative_coord(0, 0): THIS_ID(spritelayout_2);
	relative_coord(0, 1): THIS_ID(spritelayout_1);
	relative_coord(0, 2): THIS_ID(spritelayout_5);
	relative_coord(0, 3): THIS_ID(spritelayout_6);
	relative_coord(1, 0): THIS_ID(spritelayout_8);
	relative_coord(1, 1): THIS_ID(spritelayout_8);
	relative_coord(1, 2): THIS_ID(spritelayout_large_shed);
	relative_coord(1, 3): THIS_ID(spritelayout_6);
	relative_coord(2, 0): THIS_ID(spritelayout_5);
	relative_coord(2, 1): THIS_ID(spritelayout_6);
	relative_coord(2, 2): THIS_ID(spritelayout_large_shed);
	relative_coord(2, 3): THIS_ID(spritelayout_7);
	THIS_ID(spritelayout_9);
}

switch(FEAT_INDUSTRYTILES, SELF, THIS_ID(layout_3), relative_pos) {
	relative_coord(0, 0): THIS_ID(spritelayout_6);
	relative_coord(0, 1): THIS_ID(spritelayout_large_shed);
	relative_coord(0, 2): THIS_ID(spritelayout_2);
	relative_coord(0, 3): THIS_ID(spritelayout_1);
	relative_coord(0, 4): THIS_ID(spritelayout_8);
	relative_coord(1, 0): THIS_ID(spritelayout_8);
	relative_coord(1, 1): THIS_ID(spritelayout_large_shed);
	relative_coord(1, 2): THIS_ID(spritelayout_6);
	relative_coord(1, 3): THIS_ID(spritelayout_5);
	relative_coord(1, 4): THIS_ID(spritelayout_7);
	THIS_ID(spritelayout_9);
}

switch(FEAT_INDUSTRYTILES, SELF, THIS_ID(layout_4), relative_pos) {
	relative_coord(0, 0): THIS_ID(spritelayout_large_shed);
	relative_coord(0, 1): THIS_ID(spritelayout_7);
	relative_coord(1, 0): THIS_ID(spritelayout_large_shed);
	relative_coord(1, 1): THIS_ID(spritelayout_6);
	relative_coord(2, 0): THIS_ID(spritelayout_2);
	relative_coord(2, 1): THIS_ID(spritelayout_1);
	relative_coord(3, 0): THIS_ID(spritelayout_8);
	relative_coord(3, 1): THIS_ID(spritelayout_6);
	relative_coord(4, 0): THIS_ID(spritelayout_5);
	relative_coord(4, 1): THIS_ID(spritelayout_6);
	THIS_ID(spritelayout_9);
}

switch(FEAT_INDUSTRYTILES, PARENT, THIS_ID(tile_spritelayout), layout_num) {
	1: THIS_ID(layout_1);
	2: THIS_ID(layout_2);
	3: THIS_ID(layout_3);
	4: THIS_ID(layout_4);
	THIS_ID(spritelayout_9);
}

FENCES_EXCLUDE_BY_CLASS( THIS_ID(tile_fence_station),  THIS_ID(tile_spritelayout),  TILE_CLASS_STATION)
FENCES_EXCLUDE_BY_CLASS( THIS_ID(tile_fence_industry), THIS_ID(tile_fence_station), TILE_CLASS_INDUSTRY)
ADD_FLAT_FENCES(THIS_ID(tile_fences), THIS_ID(tile_fence_industry), 1, 1, 1, 1)

/* *************************************************
 * Definition of the industry tile and its callbacks
 * *************************************************/

TILE_DISALLOW_NEARBY_CLASS(tile_nearby_industry, TILE_CLASS_INDUSTRY, CB_RESULT_LOCATION_DISALLOW, return CB_RESULT_LOCATION_ALLOW)
TILE_ALLOW_PLAYER         (tile_location_check, THIS_ID(tile_nearby_industry))

item(FEAT_INDUSTRYTILES, THIS_ID(tile), TILE_LUMBERYARD_1) {
	property {
		substitute:       0;
		accepted_cargos:  [[WDPR, 8], [RFPR, 8]];
		land_shape_flags: 0;
	}

	graphics {
		tile_check:   THIS_ID(tile_location_check);
		THIS_ID(tile_fences);
	}
}


/* The kiln building must not build on slopes.  Use a separate tile to achieve that.  Chains to same other checks, graphics etc. */
TILE_DISALLOW_SLOPES      (tile2_location_check, CB_RESULT_LOCATION_DISALLOW, THIS_ID(tile_location_check))

item(FEAT_INDUSTRYTILES, THIS_ID(kiln_building_tile), TILE_LUMBERYARD_2) {
	property {
		substitute:       0;
		accepted_cargos:  [[WDPR, 8], [RFPR, 8]];
		land_shape_flags: bitmask(LSF_ONLY_ON_FLAT_LAND);
	}

	graphics {
		tile_check:   THIS_ID(tile2_location_check);
		THIS_ID(tile_fences);
	}
}

/* *************************************************
 * Definition of the industry layouts
 * *************************************************/

tilelayout tilelayout_lumber_yard_1 {
	0, 0: THIS_ID(kiln_building_tile);
	0, 1: THIS_ID(kiln_building_tile);
	0, 2: THIS_ID(tile);
	1, 0: THIS_ID(tile);
	1, 1: THIS_ID(tile);
	1, 2: THIS_ID(tile);
	2, 0: THIS_ID(tile);
	2, 1: THIS_ID(tile);
	2, 2: THIS_ID(tile);
	3, 0: THIS_ID(tile);
	3, 1: THIS_ID(tile);
	3, 2: THIS_ID(tile);
}

tilelayout tilelayout_lumber_yard_2 {
	0, 0: THIS_ID(kiln_building_tile);
	0, 1: THIS_ID(kiln_building_tile);
	0, 2: THIS_ID(tile);
	0, 3: THIS_ID(tile);
	1, 0: THIS_ID(tile);
	1, 1: THIS_ID(tile);
	1, 2: THIS_ID(tile);
	1, 3: THIS_ID(tile);
	2, 0: THIS_ID(tile);
	2, 1: THIS_ID(tile);
	2, 2: THIS_ID(tile);
	2, 3: THIS_ID(tile);
}

tilelayout tilelayout_lumber_yard_3 {
	0, 0: THIS_ID(tile);
	0, 1: THIS_ID(tile);
	0, 2: THIS_ID(kiln_building_tile);
	0, 3: THIS_ID(kiln_building_tile);
	0, 4: THIS_ID(tile);
	1, 0: THIS_ID(tile);
	1, 1: THIS_ID(tile);
	1, 2: THIS_ID(tile);
	1, 3: THIS_ID(tile);
	1, 4: THIS_ID(tile);
}

tilelayout tilelayout_lumber_yard_4 {
	0, 0: THIS_ID(tile);
	0, 1: THIS_ID(tile);
	1, 0: THIS_ID(tile);
	1, 1: THIS_ID(tile);
	2, 0: THIS_ID(kiln_building_tile);
	2, 1: THIS_ID(kiln_building_tile);
	3, 0: THIS_ID(tile);
	3, 1: THIS_ID(tile);
	4, 0: THIS_ID(tile);
	4, 1: THIS_ID(tile);
}

/* *************************************************
 * Definition of the industry
 * *************************************************/

/* for secondary production template */
#define THIS_NUM_OUTPUT_CARGOS    2
#define THIS_PROD_RATIO_CARGO1    6
#define THIS_PROD_RATIO_CARGO2    2
#define THIS_PROD_RATIO_CARGO3    0
#define THIS_BOOST_12             6
#define THIS_BOOST_13             0
#define THIS_BOOST_21             2
#define THIS_BOOST_23             0
#define THIS_BOOST_31             0
#define THIS_BOOST_32             0
/* extra text template */
#define THIS_STR_EXTRA_TEXT       STR_EXTRA_LUMBER_YARD
#define THIS_STR_DEBUG_TEXT       STR_EXTRA_TEXT_DEBUG_SECONDARY


CHECK_INCOMPATIBLE(lumber_yard, 56, CB_RESULT_LOCATION_DISALLOW, return CB_RESULT_LOCATION_ALLOW)
CHECK_FOUNDER     (THIS_ID(lumber_yard))

SWITCH_COLOUR_SCHEME1(THIS_ID(colour))

#include "../templates/produce_secondary.pnml"
#include "../templates/check_secondary_production_level.pnml"
#include "../templates/check_secondary_closure.pnml"

#include "../templates/extra_text_secondary.pnml"

#include "../templates/check_availability.pnml"

item(FEAT_INDUSTRIES, lumber_yard, IND_LUMBERYARD_ID) {
	property {
		substitute: 0;
		layouts: [
				tilelayout_lumber_yard_1,
				tilelayout_lumber_yard_2,
				tilelayout_lumber_yard_3,
				tilelayout_lumber_yard_4
			];
		life_type:              IND_LIFE_TYPE_PROCESSING;
		closure_msg:            TTD_STR_NEWS_INDUSTRY_CLOSURE_SUPPLY_PROBLEMS;
		prod_increase_msg:      TTD_STR_NEWS_INDUSTRY_PRODUCTION_INCREASE_GENERAL;
		prod_decrease_msg:      TTD_STR_NEWS_INDUSTRY_PRODUCTION_DECREASE_GENERAL;
		new_ind_msg:            TTD_STR_NEWS_INDUSTRY_CONSTRUCTION;
		name:                   string(STR_IND_LUMBER_YARD);
		nearby_station_name:    string(STR_STATION, string(STR_TOWN), string(STR_IND_LUMBER_YARD));
		prod_cargo_types: [ENSP, BDMT];
		accept_cargo_types: [WDPR, RFPR];
		prod_multiplier: [0, 0];
		input_multiplier_1: [0, 0];
		input_multiplier_2: [0, 0];
		input_multiplier_3: [0, 0];
		min_cargo_distr: 5;
		conflicting_ind_types: [];
		prob_random: 5;
		prob_in_game: 3;
		prospect_chance: 0.75;
		fund_cost_multiplier: 35;
		map_colour: 42;
		remove_cost_multiplier: 0;
		spec_flags:             0;
	}
	graphics {
		produce_cargo_arrival:   THIS_ID(produce);
		construction_probability:THIS_ID(check_availability);
		location_check:          THIS_ID(check_location);
		monthly_prod_change:     THIS_ID(check_production_level);
		random_prod_change:      THIS_ID(check_closure);
		extra_text_industry:     THIS_ID(extra_text);
		colour:                  THIS_ID(colour);
	}
}

#include "../templates/undefs.pnml"
